# OauthToken

/now = int date "X"

/client = query {
  item: OauthClient(id: /client_id) {
    id
    secret
    type
    trusted
  }
} pick /item

/ = (/client/type == "confidential") then {{
  # Check the client authorization.
  # This can be done via HTTP basic authentication or the client_secret field.
  /allow = (/headers/authorization == ("Basic " + base64 /client/id + ":" + /client/secret)) or /client_secret == /client/secret
  (!/allow) then error {type: "Unauthorized"}
}}

/data/status = 400

/ = (/grant_type == "authorization_code") then {{
  /code = /code split "."
  /valid = /code/2 == base64urlFromBase64 signature /code/0 + "." + /code/1
  / = /valid then {{
    /payload = fromJson fromBase64url /code/1
    debug /payload
    /valid = (/payload/exp > int date "X") and (/payload/aud == /client/id)
  }}

  / = /valid then {{

    # Check if an access token already exists.
    # Only remember login when token is still valid for at least 15 minutes.
    /tokens = query {
      tokens: listOauthAccessToken(filters: {
        client: /payload/aud,
        user: /payload/sub,
        scope: /payload/scope,
        expires_gt: (/now + 900)
      }) {
        items {
          id
          token
          expires
          user { id }
          client { id }
        }
      }
    } pick /tokens/items

    / = /tokens/0/id then {{
      /data/body = {
        access_token: /tokens/0/token,
        token_type: "bearer",
        user_id: /tokens/0/user/id,
        expires_in: /tokens/0/expires - /now
      }
      # Also re-use the refresh token for confidential clients.
      / = (/tokens/0/client/type == "confidential") then {{
        /data/body/refresh_token = query {
          tokens: listOauthRefreshToken(filters: {
            client: /payload/aud,
            user: /payload/sub,
            scope: /payload/scope,
            expires_gt: /now + 900
          }) { items { token } }
        } pick /tokens/items/0/token
      }}
    }}

    / = (!/tokens/0/id) then {{
      # Create a new access token.
      /token = {
        client: /client,
        user: {id: /payload/sub},
        scope: /payload/scope
      } script "OauthCreateToken"

      /data/body = {
        access_token: /token/token,
        token_type: "bearer",
        user_id: /token/user,
        expires_in: /token/expires - /now
      }
      / = /token/refresh_token then {{
        /data/body/refresh_token = /token/refresh_token
      }}
    }}

    /data/status = 200
  }}

}}

/ = (/grant_type == "refresh_token") then {{
  /refresh_tokens = query {
    tokens: listOauthRefreshToken(filters: {
      token: /refresh_token,
      client: /client/id,
      expires_gte: /now
    }) {
      items {
        id
        scope
        user { id }
        client { id type }
      }
    }
  } pick /tokens/items

  / = /refresh_tokens/0 then {{
    # Delete old refresh token.
    mutation { deleteOauthRefreshToken(id: /refresh_tokens/0/id) { id } }

    # Create a new access token.
    /token = {
      client: /client,
      user: /refresh_tokens/0/user,
      scope: /refresh_tokens/0/scope,
      forceRecreate: true
    } script "OauthCreateToken"
    /data/body = {
      access_token: /token/token,
      token_type: "bearer",
      user_id: /token/user,
      expires_in: /token/expires - /now
    }
    /data/body/refresh_token = /token/refresh_token
    /data/status = 200
  }}
}}

# Set security related headers.
/"data/headers/Cache-Control" = "no-store"
/data/headers/Pragma = "no-cache"

/ = /data
